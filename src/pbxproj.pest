WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT = _{ BLOCK_COMMENT | LINE_COMMENT }

BLOCK_COMMENT = { "/*" ~ ( (!"*/" ~ ANY) )* ~ "*/" }

LINE_COMMENT = { "//" ~ ( (!"\n" ~ ANY) )* ~ ("\n" | EOI) }

skip = _{ (" " | "\t" | "\r" | "\n")* }

padding = _{ (WHITESPACE | COMMENT)* }

file = { SOI ~ padding ~ dictionary ~ padding ~ EOI }

dictionary = { "{" ~ padding ~ pair_sequence? ~ padding ~ "}" }

pair_sequence = { pair_entry ~ (";" ~ padding ~ pair_entry)* ~ ";"? }

pair_entry = { padding ~ pair ~ pair_comment? ~ skip }

pair = { key ~ skip ~ "=" ~ skip ~ value }

key = { string | identifier }

value = { dictionary | array | string | identifier | number }

string = @{ "\"" ~ string_inner* ~ "\"" }

string_inner = _{ escape | (!"\"" ~ ANY) }

escape = _{ "\\" ~ ( unicode | "\"" | "\\" | "b" | "f" | "n" | "r" | "t" ) }

unicode = { "u" ~ ASCII_HEX_DIGIT ~ ASCII_HEX_DIGIT ~ ASCII_HEX_DIGIT ~ ASCII_HEX_DIGIT }

array = { "(" ~ padding ~ value_list? ~ padding ~ ")" }

value_list = { value_entry ~ ("," ~ padding ~ value_entry)* ~ ","? }

value_entry = { padding ~ value ~ padding }

identifier = @{ identifier_start ~ identifier_char* }

identifier_start = _{ ASCII_ALPHANUMERIC | "_" | "/" }

identifier_char = _{ ASCII_ALPHANUMERIC | "_" | "." | "+" | "-" | "/" | "$" }

number = { "-"? ~ ASCII_DIGIT+ }

pair_comment = { skip ~ inline_comment }

inline_comment = { BLOCK_COMMENT | LINE_COMMENT }
